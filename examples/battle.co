(define if [(cond body)
	(cond match {
		((true) (body :))
		((false))
	})
])
(define forever [(body)
	(body :)
	(forever : body)
])

(define zombie {
	((attacking initial-target)
		(define health (cell new 20))
		(define target (cell new initial-target))
		(define self {
			((hit-by attacker power)
				(if : (health > 0) [()
					(console print "Eugh!")
					(console read)
					(health := (health - power))
					(target := attacker)
					(if : (health <= 0) [()
						(console print "The zombie falls over!")
					])
				])
			)
			((your-turn)
				(if : (health > 0) [()
					(console print ("The zombie attacks the " .. (target type) "!"))
					(target hit-by self 1)
				])
			)
			((type) "zombie")
		})
	)
})
(define player ([()
	(define health (cell new 10))
	(define target (cell new))
	(define self {
		((hit-by attacker power)
			(if : (health > 0) [()
				(console print ("You took " .. power " damage from a " (attacker type) "!"))
				(console read)
				(health := (health - power))
				(target := attacker)
			])
		)
		((your-turn)
			(console print ("Health: " .. health))
			((health > 0) match {
				((true)
					(console print "What do you do?")
					(define input (console read))
					(if : (input = "attack") [()
						(console print ("You attack the " .. (target type) "!"))
						(target hit-by self 5)
					])
				)
				((false)
					(console print "You are dead, F.")
					(console read)
				)
			})
		)
		((type) "player")
	})
] :))

(define a-zombie (zombie attacking player))
(forever : [()
	(a-zombie your-turn)
	(player your-turn)
])